def l_table_owner="&1."
def l_table_name="&2."
def l_tablespace_name="&3."
def l_table_name_int="&4."
def l_tablespace_name_int="&5."
def l_degree="&6."
def l_resumable_timeout="&7."
def l_parallel_min_pct="&8."

exec dbms_application_info.set_action( 'COPY_INDEX' )

declare
  subtype name_type is varchar2(30);
  l_table_owner         name_type := '&l_table_owner.';
  l_table_name          name_type := '&l_table_name.';
  l_table_name_int      name_type := '&l_table_name_int.';
  l_tablespace_name     name_type := '&l_tablespace_name.';
  l_tablespace_name_int name_type := '&l_tablespace_name_int.';
  l_degree              int       := &l_degree.;
	l_parallel_min_pct_old int;
	l_parallel_min_pct_new int := &l_parallel_min_pct.;
  l_resumable_timeout   int       := &l_resumable_timeout.;
  -- local vars
  l_handle pls_integer;
  l_tr_handle pls_integer;
  l_ddls sys.ku$_ddls;
  l_start_time int;
  l_index_name name_type;
  procedure print_lob(p_clob in clob)
  is
    l_linesize pls_integer := 255;
    l_buffer   varchar2(255);
    l_length   pls_integer;
  begin
    l_length := dbms_lob.getlength(p_clob);
    for i in 0..floor(l_length/l_linesize)
    loop
      l_buffer := dbms_lob.substr(p_clob, l_linesize, 1+l_linesize*i);
      dbms_output.put_line(l_buffer);
    end loop;
  end print_lob;
begin 
  if l_degree > 1 
  then
	  select value
		  into l_parallel_min_pct_old
			from v$parameter
		 where name = 'parallel_min_percent';
    execute immediate 'ALTER SESSION FORCE PARALLEL DML PARALLEL ' || l_degree;
    execute immediate 'ALTER SESSION FORCE PARALLEL DDL PARALLEL ' || l_degree;
    execute immediate 'ALTER SESSION FORCE PARALLEL QUERY PARALLEL ' || l_degree;
		execute immediate 'ALTER SESSION SET PARALLEL_MIN_PERCENT=' || l_parallel_min_pct_new;
  end if; 
  execute immediate 'ALTER SESSION ENABLE RESUMABLE TIMEOUT '||l_resumable_timeout||' NAME ''Redef ' || l_table_name || '''';
  l_handle := sys.dbms_metadata.open( 'INDEX');
  sys.dbms_metadata.set_filter(l_handle, 'BASE_OBJECT_SCHEMA', l_table_owner);
  sys.dbms_metadata.set_filter(l_handle, 'BASE_OBJECT_NAME', l_table_name);
	sys.dbms_metadata.set_filter(l_handle, 'EXCLUDE_NAME_EXPR', 'LIKE ''SYS_IL%'''); -- exclude lob indexes
  l_tr_handle := sys.dbms_metadata.add_transform( l_handle, 'MODIFY');
  sys.dbms_metadata.set_remap_param( l_tr_handle, 'REMAP_NAME', l_table_name, l_table_name_int);
  sys.dbms_metadata.set_remap_param( l_tr_handle, 'REMAP_TABLESPACE', l_tablespace_name, l_tablespace_name_int);
  -- Remap index names
  for ind_rec in (
    select index_name
      from dba_indexes
     where table_owner = l_table_owner
       and table_name = l_table_name)
  loop
    l_index_name := substr(ind_rec.index_name, 1, 26)||'_INT';
    sys.dbms_metadata.set_remap_param( l_tr_handle, 'REMAP_NAME', ind_rec.index_name, l_index_name);
  end loop;
  --
  l_tr_handle := sys.dbms_metadata.add_transform( l_handle, 'DDL');
  sys.dbms_metadata.set_transform_param( l_tr_handle, 'PARALLEL', l_degree);
  loop
    l_ddls := sys.dbms_metadata.fetch_ddl( l_handle);
    exit when l_ddls is null;
    for i in 1..cardinality(l_ddls)
    loop
      print_lob(l_ddls(i).ddlText);
      l_start_time := dbms_utility.get_time;
      execute immediate l_ddls(i).ddlText;
      dbms_output.put_line( 'Elapsed: '||round((dbms_utility.get_time-l_start_time)/1e2));
    end loop;
  end loop;
  -- Register dependent objects and alter index degree
  for ind_rec in (
    select index_name
      from dba_indexes ind
     where ind.table_owner = l_table_owner
       and ind.table_name = l_table_name
		   and ind.index_type not in ('LOB'))
  loop
    l_index_name := substr(ind_rec.index_name, 1, 26)||'_INT';
    execute immediate 'alter index '||l_table_owner||'.'||l_index_name||' noparallel';
    sys.dbms_redefinition.register_dependent_object(
      uname         => l_table_owner,
      orig_table    => l_table_name,
      int_table     => l_table_name_int,
      dep_type      => sys.dbms_redefinition.cons_index,
      dep_owner     => l_table_owner,
      dep_orig_name => ind_rec.index_name,
      dep_int_name  => l_index_name
    );
  end loop;
  sys.dbms_metadata.close( l_handle);
  if l_degree > 1 
  then
    execute immediate 'ALTER SESSION DISABLE PARALLEL DML';
    execute immediate 'ALTER SESSION DISABLE PARALLEL DDL';
    execute immediate 'ALTER SESSION DISABLE PARALLEL QUERY';
		execute immediate 'alter session set parallel_min_percent='||l_parallel_min_pct_old;
  end if; 
end;
/
exec dbms_application_info.set_action( null )

